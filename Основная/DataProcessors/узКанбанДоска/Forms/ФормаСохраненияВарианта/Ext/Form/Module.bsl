
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ИнициализироватьФормуНаСервере(Отказ, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// [+] #283 Мальков М.В. 2023-01-30
&НаКлиенте
Процедура СписокВариантовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

// [+] #283 Мальков М.В. 2023-01-30
&НаКлиенте
Процедура СписокВариантовПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.СписокВариантов.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда 
		
		Вариант = ТекущиеДанные.Представление;
		Ключ 	= ТекущиеДанные.Значение;
		
	КонецЕсли;
	
КонецПроцедуры

// [+] #283 Мальков М.В. 2023-01-30
&НаКлиенте
Процедура ВариантПриИзменении(Элемент)
	
	КлючПоиска = "";
	
	Если ЗначениеЗаполнено(Вариант) Тогда 
		Для Каждого Строка Из СписокВариантов Цикл 
			
			Если Не ЗначениеЗаполнено(КлючПоиска)
				И Строка.Представление = Вариант Тогда 
				КлючПоиска = Строка.Значение;	
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(КлючПоиска) Тогда 
		КлючПоиска = ОсновнойКлючВарианта + "." + Вариант;	
	КонецЕсли;
	
	Ключ = КлючПоиска;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// [+] #283 Мальков М.В. 2023-01-30
&НаКлиенте
Процедура СохранитьКоманда(Команда)
	
	Если Не ЗначениеЗаполнено(Вариант) Тогда 
		ПоказатьПредупреждение(, "Не заполнено значение варианта",, "Внимание");
	ИначеЕсли СписокВариантов.НайтиПоЗначению(Ключ) <> Неопределено Тогда 
		
		Оповещение = Новый ОписаниеОповещения("СохранитьКомандаЗавершение", ЭтаФорма);
		ПоказатьВопрос(Оповещение, "Вы хотите перезаписать вариант?", РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да, "Внимание");
		
	Иначе 	 
		
		Данные = Новый Структура("Ключ, Представление", Ключ, Вариант);
		Закрыть(Данные);
		
	КонецЕсли;
		
КонецПроцедуры

// [+] #283 Мальков М.В. 2023-01-30
&НаКлиенте
Процедура СохранитьКомандаЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда 
		Данные = Новый Структура("Ключ, Представление", Ключ, Вариант);
		Закрыть(Данные);
	КонецЕсли;	

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// [+] #283 Мальков М.В. 2023-01-30
&НаСервере        
Процедура ИнициализироватьФормуНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОсновнойКлючВарианта", ОсновнойКлючВарианта);
	Параметры.Свойство("ТекущийКлючВарианта", ТекущийКлючВарианта);
	
	СписокВариантов = Обработки.узКанбанДоска.ПолучитьСписокВариантов(ОсновнойКлючВарианта);
	
	Если СписокВариантов.Количество() > 0
		И СписокВариантов.НайтиПоЗначению(ТекущийКлючВарианта) <> Неопределено Тогда 
		
		Строка 	= СписокВариантов.НайтиПоЗначению(ТекущийКлючВарианта);
		Вариант = Строка.Представление;	
		Ключ	= ТекущийКлючВарианта;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

