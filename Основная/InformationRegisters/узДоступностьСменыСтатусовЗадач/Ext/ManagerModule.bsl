#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// [+] #285 Павлюков С.Ю. 2024-04-14
// Возвращает массив доступных статусов для проекта и текущего статуса (источника) 
//
// Параметры:
//	Проект - СправочникСсылка.узПроекты - проект задачи
//	СтатусИсточник - СправочникСсылка.узСтатусыЗадачи - текущий статус задачи
//	
// Возвращаемое значение:
//	Массив - список доступных статусов
//
Функция ДоступныеДляСменыСтатусы(Проект, СтатусИсточник) Экспорт
	
	Результат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ДоступныеДляСменыСтатусы_ТекстЗапроса();
	
	Запрос.УстановитьПараметр("Проект"			, Проект);
	Запрос.УстановитьПараметр("СтатусИсточник"	, СтатусИсточник);
	Запрос.УстановитьПараметр("Пользователь"	, Пользователи.ТекущийПользователь());
	
	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("СтатусПриемник");
	
	Возврат Результат;
	
КонецФункции

// [+] #285 Павлюков С.Ю. 2024-04-14
// Проверяет и оставляет в списке задач только доступные для смены на новый статус 
//
// Параметры:
//	МассивЗадач - Массив - элементы СправочникСсылка.узЗадачи
//	НовыйСтатус - СправочникСсылка.узСтатусыЗадачи - новый статус задачи
//
Процедура ОставитьЗадачиДляКоторыхДоступенНовыйСтатус(МассивЗадач, НовыйСтатус) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = ОставитьЗадачиДляКоторыхДоступенНовыйСтатус_ТекстЗапроса();
	Запрос.УстановитьПараметр("МассивЗадач"		, МассивЗадач);
	Запрос.УстановитьПараметр("СтатусПриемник"	, НовыйСтатус);
	Запрос.УстановитьПараметр("Пользователь"	, Пользователи.ТекущийПользователь());
	
	//смотрим данные всех задач сразу: необходимость контроля, доступные статусы
	данныеЗапроса = Запрос.Выполнить().Выгрузить();
	
	массивУдалить = Новый Массив;
	Для Каждого Задача Из МассивЗадач Цикл
		
		ПараметрыОтбора = Новый Структура();
		ПараметрыОтбора.Вставить("Задача", Задача);

		массивНайдено = данныеЗапроса.НайтиСтроки(ПараметрыОтбора);
		
		Если массивНайдено.Количество() > 0
			И массивУдалить.Найти(Задача) = Неопределено Тогда
			массивУдалить.Добавить(Задача);
		КонецЕсли;
		
		Для Каждого СтрокаданныеЗапроса Из массивНайдено Цикл
			
			ТекстСообщения = НСтр("ru = 'Запрещено указавать статус [%1] для задачи [%2] в настройках проекта [%3]'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, НовыйСтатус, Задача, СтрокаданныеЗапроса.Проект);
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = ТекстСообщения;
			Сообщение.Сообщить();
						
		КонецЦикла;
		
	КонецЦикла;
	
	Для Каждого элементУдалить Из массивУдалить Цикл
		ИндексЗадачи = МассивЗадач.Найти(элементУдалить);
		МассивЗадач.Удалить(ИндексЗадачи);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий
// Код процедур и функций
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// [+] #285 Павлюков С.Ю. 2024-04-14
Функция ДоступныеДляСменыСтатусы_ТекстЗапроса() 
	
	Возврат "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	узСтатусыЗадачи.Ссылка КАК СтатусПриемник
	|ИЗ
	|	Справочник.узСтатусыЗадачи КАК узСтатусыЗадачи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			узДоступностьСменыСтатусовЗадач.СтатусПриемник КАК СтатусПриемник
	|		ИЗ
	|			РегистрСведений.узДоступностьСменыСтатусовЗадач КАК узДоступностьСменыСтатусовЗадач
	|		ГДЕ
	|			узДоступностьСменыСтатусовЗадач.Проект = &Проект
	|			И узДоступностьСменыСтатусовЗадач.ПользовательГруппа = &Пользователь
	|			И (узДоступностьСменыСтатусовЗадач.СтатусИсточник = &СтатусИсточник
	|					ИЛИ узДоступностьСменыСтатусовЗадач.СтатусИсточник = ЗНАЧЕНИЕ(Справочник.узСтатусыЗадачи.ПустаяСсылка))
	|			И ТИПЗНАЧЕНИЯ(узДоступностьСменыСтатусовЗадач.ПользовательГруппа) = ЗНАЧЕНИЕ(Справочник.Пользователи)
	|		
	|		ОБЪЕДИНИТЬ
	|		
	|		ВЫБРАТЬ
	|			узДоступностьСменыСтатусовЗадач.СтатусПриемник
	|		ИЗ
	|			РегистрСведений.узДоступностьСменыСтатусовЗадач КАК узДоступностьСменыСтатусовЗадач
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ГруппыПользователей.Состав КАК ГруппыПользователейСостав
	|				ПО узДоступностьСменыСтатусовЗадач.ПользовательГруппа = ГруппыПользователейСостав.Ссылка
	|					И (&Пользователь = ГруппыПользователейСостав.Пользователь)
	|		ГДЕ
	|			узДоступностьСменыСтатусовЗадач.Проект = &Проект
	|			И (узДоступностьСменыСтатусовЗадач.СтатусИсточник = &СтатусИсточник
	|					ИЛИ узДоступностьСменыСтатусовЗадач.СтатусИсточник = ЗНАЧЕНИЕ(Справочник.узСтатусыЗадачи.ПустаяСсылка))
	|			И ТИПЗНАЧЕНИЯ(узДоступностьСменыСтатусовЗадач.ПользовательГруппа) = ЗНАЧЕНИЕ(Справочник.ГруппыПользователей)) КАК ВложРег
	|		ПО (узСтатусыЗадачи.Ссылка = ВложРег.СтатусПриемник
	|				ИЛИ ВложРег.СтатусПриемник = ЗНАЧЕНИЕ(Справочник.узСтатусыЗадачи.ПустаяСсылка))	
	|";
	
КонецФункции 

// [+] #285 Павлюков С.Ю. 2024-04-14
Функция ОставитьЗадачиДляКоторыхДоступенНовыйСтатус_ТекстЗапроса() 
	
	Возврат "ВЫБРАТЬ
	|	узЗадачи.Ссылка КАК Задача,
	|	узЗадачи.Статус КАК СтатусИсточник,
	|	ВЫБОР узЗадачи.ОсновнаяЗадача
	|		КОГДА ЗНАЧЕНИЕ(Справочник.узЗадачи.ПустаяСсылка)
	|			ТОГДА узЗадачи.Проект
	|		ИНАЧЕ узЗадачи.ОсновнаяЗадача.Проект
	|	КОНЕЦ КАК Проект
	|ПОМЕСТИТЬ ВТ_Задачи
	|ИЗ
	|	Справочник.узЗадачи КАК узЗадачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.узПроекты КАК узПроекты
	|		ПО (ВЫБОР узЗадачи.ОсновнаяЗадача
	|				КОГДА ЗНАЧЕНИЕ(Справочник.узЗадачи.ПустаяСсылка)
	|					ТОГДА узЗадачи.Проект
	|				ИНАЧЕ узЗадачи.ОсновнаяЗадача.Проект
	|			КОНЕЦ = узПроекты.Ссылка)
	|ГДЕ
	|	узЗадачи.Ссылка В(&МассивЗадач)
	|	И ЕСТЬNULL(узПроекты.ОграничиватьСменуСтатусов, ЛОЖЬ) = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	узСтатусыЗадачи.Ссылка КАК СтатусИсточник
	|ПОМЕСТИТЬ ВТ_Источники
	|ИЗ
	|	Справочник.узСтатусыЗадачи КАК узСтатусыЗадачи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			узДоступностьСменыСтатусовЗадач.СтатусИсточник КАК СтатусИсточник
	|		ИЗ
	|			РегистрСведений.узДоступностьСменыСтатусовЗадач КАК узДоступностьСменыСтатусовЗадач
	|		ГДЕ
	|			узДоступностьСменыСтатусовЗадач.Проект В
	|					(ВЫБРАТЬ
	|						ВТ_Задачи.Проект
	|					ИЗ
	|						ВТ_Задачи)
	|			И узДоступностьСменыСтатусовЗадач.ПользовательГруппа = &Пользователь
	|			И (узДоступностьСменыСтатусовЗадач.СтатусПриемник = &СтатусПриемник
	|					ИЛИ узДоступностьСменыСтатусовЗадач.СтатусПриемник = ЗНАЧЕНИЕ(Справочник.узСтатусыЗадачи.ПустаяСсылка))
	|			И ТИПЗНАЧЕНИЯ(узДоступностьСменыСтатусовЗадач.ПользовательГруппа) = ЗНАЧЕНИЕ(Справочник.Пользователи)
	|		
	|		ОБЪЕДИНИТЬ
	|		
	|		ВЫБРАТЬ
	|			узДоступностьСменыСтатусовЗадач.СтатусИсточник
	|		ИЗ
	|			РегистрСведений.узДоступностьСменыСтатусовЗадач КАК узДоступностьСменыСтатусовЗадач
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ГруппыПользователей.Состав КАК ГруппыПользователейСостав
	|				ПО узДоступностьСменыСтатусовЗадач.ПользовательГруппа = ГруппыПользователейСостав.Ссылка
	|					И (&Пользователь = ГруппыПользователейСостав.Пользователь)
	|		ГДЕ
	|			узДоступностьСменыСтатусовЗадач.Проект В
	|					(ВЫБРАТЬ
	|						ВТ_Задачи.Проект
	|					ИЗ
	|						ВТ_Задачи)
	|			И (узДоступностьСменыСтатусовЗадач.СтатусПриемник = &СтатусПриемник
	|					ИЛИ узДоступностьСменыСтатусовЗадач.СтатусПриемник = ЗНАЧЕНИЕ(Справочник.узСтатусыЗадачи.ПустаяСсылка))
	|			И ТИПЗНАЧЕНИЯ(узДоступностьСменыСтатусовЗадач.ПользовательГруппа) = ЗНАЧЕНИЕ(Справочник.ГруппыПользователей)) КАК ВложРег
	|		ПО (узСтатусыЗадачи.Ссылка = ВложРег.СтатусИсточник
	|				ИЛИ ВложРег.СтатусИсточник = ЗНАЧЕНИЕ(Справочник.узСтатусыЗадачи.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Задачи.Задача КАК Задача,
	|	ВТ_Задачи.СтатусИсточник КАК СтатусИсточник,
	|	ВТ_Задачи.Проект КАК Проект
	|ИЗ
	|	ВТ_Задачи КАК ВТ_Задачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Источники КАК ВТ_Источники
	|		ПО ВТ_Задачи.СтатусИсточник = ВТ_Источники.СтатусИсточник
	|ГДЕ
	|	ВТ_Источники.СтатусИсточник ЕСТЬ NULL";
		
КонецФункции 

#КонецОбласти

#КонецЕсли




