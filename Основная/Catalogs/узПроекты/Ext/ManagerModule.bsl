#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

Процедура СоздатьПроектыПоОсновнымЗадачам() Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	узЗадачи.Ссылка КАК Задача,
	|	узЗадачи.Проект КАК Проект,
	|	узЗадачи.ЭтоОсновнаяЗадача КАК ЭтоОсновнаяЗадача,
	|	узЗадачи.ОсновнаяЗадача КАК ОсновнаяЗадача
	|ИЗ
	|	Справочник.узЗадачи КАК узЗадачи
	|ГДЕ
	|	НЕ узЗадачи.ПометкаУдаления
	|	И узЗадачи.Проект = ЗНАЧЕНИЕ(Справочник.узПроекты.ПустаяСсылка)
	|	И узЗадачи.ЭтоОсновнаяЗадача";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда   
		
		ТекстСообщения = НСтр("ru = 'Создание проектов не требуется. По всем основным задачам проекты уже созданы '");
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ТекстСообщения;
		Сообщение.Сообщить();
		
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ЗадачаСсылка = Выборка.Задача;
		ОсновнаяЗадача = Выборка.ОсновнаяЗадача;
		
		НачатьТранзакцию();	
		
		НаименованиеПроекта = "" + ЗадачаСсылка;
		Если ЗначениеЗаполнено(ОсновнаяЗадача) Тогда
			ПроектСсылка = ОсновнаяЗадача.Проект;
		Иначе	
			ПроектСсылка = СоздатьПроект(НаименованиеПроекта); 
		КонецЕсли;
		
		СпрОбъектЗадача = ЗадачаСсылка.ПолучитьОбъект();
		Попытка
			СпрОбъектЗадача.Заблокировать();
		Исключение
			
			Если ТранзакцияАктивна() Тогда
				ОтменитьТранзакцию(); 
				
				ТекстСообщения = НСтр("ru = 'Ошибка! не удалось заблокировать основную задачу [%1] описание ошибки [%2] '");
				ТекстСообщения = СтрШаблон(ТекстСообщения, ЗадачаСсылка, ОписаниеОшибки());
				ВызватьИсключение ТекстСообщения; 
				
			КонецЕсли;            
			
		КонецПопытки;		

		СпрОбъектЗадача.Проект = ПроектСсылка;		
		СпрОбъектЗадача.Записать(); 
		
		НаименованиеЗадачи = Справочники.узЗадачи.ПолучитьНомерЗадачи(ЗадачаСсылка) + " " + ЗадачаСсылка;
		
		ТекстСообщения = НСтр("ru = 'Указан проект [%1] в основной задаче [%2]'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, НаименованиеПроекта, НаименованиеЗадачи);
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ТекстСообщения;
		Сообщение.Сообщить();
		
		ЗафиксироватьТранзакцию();		
			
	КонецЦикла;
	
КонецПроцедуры 

Функция СоздатьПроект(НаименованиеПроекта) Экспорт
	
	Перем ПроектСсылка;
	
	СпрОбъектПроекты = Справочники.узПроекты.СоздатьЭлемент();
	СпрОбъектПроекты.Наименование = НаименованиеПроекта;
	СпрОбъектПроекты.Записать();
	
	ПроектСсылка = СпрОбъектПроекты.Ссылка;

    Возврат ПроектСсылка;
	
КонецФункции 

#КонецОбласти

#КонецЕсли
