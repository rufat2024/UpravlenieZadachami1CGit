
&НаКлиенте
Процедура Старт(Команда)
	НайденныеСтроки = Объект.Факт.НайтиСтроки(Новый Структура("ДатаОкончанияФакт", Дата(1,1,1)));
	Для Каждого СтрокаТЧ Из НайденныеСтроки Цикл
		СтрокаТЧ.ДатаОкончанияФакт = ТекущаяДата();
		СтрокаТЧ.Примечание = Объект.Примечание;
	КонецЦикла;
	НС = Объект.Факт.Добавить();
	НС.ДатаНачалаФакт = ТекущаяДата();
	Записать();
КонецПроцедуры

&НаКлиенте
Процедура Стоп(Команда)
	НайденныеСтроки = Объект.Факт.НайтиСтроки(Новый Структура("ДатаОкончанияФакт", Дата(1,1,1)));
	Для Каждого СтрокаТЧ Из НайденныеСтроки Цикл
		СтрокаТЧ.ДатаОкончанияФакт = ТекущаяДата();
	КонецЦикла;
	Записать();
	СписокВариантов = Новый СписокЗначений;
	СписокВариантов.Добавить("ПоПлану", "По плану ("+Объект.ВремяПлановое+")");
	СписокВариантов.Добавить("ПоФакту", "По Факту ("+Объект.ВремяФактическое+")");
	СписокВариантов.Добавить("ВключаяПерерывы", "Включая перерывы ("+Объект.ВремяАстрономическое+")");
	СписокВариантов.Добавить(КодВозвратаДиалога.Отмена, "Отмена");
	ОписаниеОповещения = Новый ОписаниеОповещения("СтопЗавершение", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, "Как отразить факт работ?", СписокВариантов, 30,СписокВариантов[3].Значение,"Вариант",СписокВариантов[3].Значение);
	 
КонецПроцедуры

&НаКлиенте
Процедура СтопЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = Неопределено Тогда
		Возврат;
	ИначеЕсли Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	Форма = ПолучитьФорму("Документ.узВводФактаПоЗадаче.Форма.ФормаДокумента",,ЭтотОбъект);
	Док = Форма.Объект;
	Док.Задача = Объект.Задача;
	Док.Исполнитель = Объект.Исполнитель;
	Если Результат = "ПоПлану" Тогда
		НС = Док.Данные.Добавить();
		НС.ДатаНачала = Объект.ДатаНачалаПлан;
		НС.ДатаОкончания  = Объект.ДатаОкончанияПлан;
		НС.Факт = Объект.ВремяПлановое;
		НС.Спринт = Объект.Спринт;
		НС.Примечание = Объект.Примечание;
	ИначеЕсли Результат = "ПоФакту" Тогда
		Для Каждого СтрокаФакт Из Объект.Факт Цикл
			НС = Док.Данные.Добавить();
			НС.ДатаНачала = СтрокаФакт.ДатаНачалаФакт;
			НС.ДатаОкончания  = СтрокаФакт.ДатаОкончанияФакт;
			НС.Факт = СтрокаФакт.Факт / 3600;
			НС.Спринт = Объект.Спринт;
			НС.Примечание = СтрокаФакт.Примечание;
		КонецЦикла;
	ИначеЕсли Результат = "ВключаяПерерывы" Тогда
		Минимум = Дата(1,1,1);
		Максимум = Дата(1,1,1);
		Для Каждого СтрокаТЧ Из Объект.Факт Цикл
			Если Минимум = Дата(1,1,1) Тогда
				Минимум = СтрокаТЧ.ДатаНачалаФакт;
			Иначе
				Минимум = Мин(Минимум,СтрокаТЧ.ДатаНачалаФакт);
			КонецЕсли;
			Если СтрокаТЧ.ДатаОкончанияФакт <> Дата(1,1,1) Тогда
				Максимум = Макс(Максимум, СтрокаТЧ.ДатаОкончанияФакт);
			КонецЕсли;
		КонецЦикла;
		НС = Док.Данные.Добавить();
		НС.ДатаНачала = Минимум;
		НС.ДатаОкончания  = Максимум;
		НС.Факт = Объект.ВремяАстрономическое;
		НС.Спринт = Объект.Спринт;
		НС.Примечание = Объект.Примечание;
	КонецЕсли;
	Форма.Открыть();
	
КонецПроцедуры


&НаКлиенте
Процедура Пауза(Команда)
	НайденныеСтроки = Объект.Факт.НайтиСтроки(Новый Структура("ДатаОкончанияФакт", Дата(1,1,1)));
	Для Каждого СтрокаТЧ Из НайденныеСтроки Цикл
		СтрокаТЧ.ДатаОкончанияФакт = ТекущаяДата();
	КонецЦикла;
	Записать();
КонецПроцедуры
