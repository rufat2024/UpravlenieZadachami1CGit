Функция СформироватьНастройкиОтображения() Экспорт
	
	НастройкиОтображения = Новый Структура(
	"ПериодОтображения,ОтображаемаяДата,ВыделенныеДаты,ОтображатьВремяС,ОтображатьВремяПо,
	|РабочиеДниНедели,ВариантПоказателя,РазмерЯчейкиВремени,ОтображатьОтмененные,Исполнители,ПоложениеШкалы");
	
	Возврат НастройкиОтображения;
	
КонецФункции

// Процедура отображает события пользователя в календарь на указанный период
Процедура ОтобразитьКалендарь(Планировщик, НастройкиОтображения) Экспорт
	
	ЗагрузитьДанныеПланировщика(Планировщик, НастройкиОтображения);
	НастройкаПланировщикаКлиентСервер.ПрименитьНастройкиОтображения(Планировщик, НастройкиОтображения);
	НастройкаПланировщикаКлиентСервер.ПрименитьОформлениеКоВсем(Планировщик, НастройкиОтображения);
	
КонецПроцедуры

Процедура ЗагрузитьДанныеПланировщика(Планировщик, НастройкиОтображения)
	
	#Если Сервер И НЕ Сервер Тогда
		Планировщик = Новый Планировщик;
	#КонецЕсли
	Планировщик.Элементы.Очистить();
	Планировщик.ИнтервалыФона.Очистить();
	Планировщик.Измерения.Очистить();
	ПериодОтображения = НастройкаПланировщикаКлиентСервер.ПолучитьПериодОтображения(НастройкиОтображения);
	ДатаНачала = НастройкаПланировщикаКлиентСервер.ПолучитьДатуНачалаОтображаемогоПериода(ПериодОтображения,НастройкиОтображения.ОтображаемаяДата);
	ДатаОкончания = НастройкаПланировщикаКлиентСервер.ПолучитьДатуОкончанияОтображаемогоПериода(ПериодОтображения,НастройкиОтображения.ОтображаемаяДата);
	ВыделенныеДаты = НастройкаПланировщикаКлиентСервер.ПолучитьВыделенныеДаты(НастройкиОтображения);
	
	Если ВыделенныеДаты.Количество() = 1 Тогда
		ВыделенныеДаты.Очистить();
		ВрДата = ДатаНачала;
		Пока ВрДата <= ДатаОкончания Цикл
			ВыделенныеДаты.Добавить(ВрДата);
			ВрДата = ВрДата + 86400;
		КонецЦикла;
	КонецЕсли;
	Исполнители = НастройкиОтображения.Исполнители;
	ИзмеренияЗаполнены = Ложь;
	МассивИсполнителей = Неопределено;
	ИзмеренияИсполнители = Планировщик.Измерения.Добавить("Исполнители");
	Если ТипЗнч(Исполнители) <> Тип("СписокЗначений") ИЛИ НЕ Исполнители.Количество() Тогда
		Исполнители = Неопределено;
	Иначе
		МассивИсполнителей = Новый Массив;
		Для Каждого Исполнитель Из Исполнители Цикл
			МассивИсполнителей.Добавить(Исполнитель.Значение);
		КонецЦикла;
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивИсполнителей,"Наименование, узКороткоеИмя",Истина);
		Сч = 0;
		Для Каждого Элемент Из Исполнители Цикл
			ИзмеренияЗаполнены = Истина;
			ТекущиииеРеквизиты = Реквизиты.Получить(Элемент.Значение);
			Если ТекущиииеРеквизиты <> Неопределено Тогда
				// Если в списке повторяющееся значение
				Если ИзмеренияИсполнители.Элементы.Найти(Элемент.Значение) <> Неопределено Тогда
					Продолжить;
				КонецЕсли;
				Измерение = ИзмеренияИсполнители.Элементы.Добавить(Элемент.Значение
				,?(ЗначениеЗаполнено(ТекущиииеРеквизиты.узКороткоеИмя), ТекущиииеРеквизиты.узКороткоеИмя, ТекущиииеРеквизиты.Наименование) // для 8.3.16 закомментировать
				);
				Если Сч%2 = 0 Тогда
					Цвет = WebЦвета.Белый;
				Иначе
					Цвет = Новый Цвет(250,250,250);
				КонецЕсли;
				Измерение.ЦветФона = Цвет;
				Сч = Сч + 1;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Запрос = Новый Запрос(ТекстЗапросаПолученияЭлементовРасписания());
	Запрос.УстановитьПараметр("НачалоПериода", ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", ДатаОкончания);
	Запрос.УстановитьПараметр("ПустаяДата", Дата(1,1,1));
	Запрос.УстановитьПараметр("Исполнители", МассивИсполнителей);
	Запрос.УстановитьПараметр("ВсеИсполнители", МассивИсполнителей = Неопределено);
	Запрос.УстановитьПараметр("Сейчас", ТекущаяДатаСеанса()); 
	Запрос.УстановитьПараметр("ОтображатьОтмененные", ?(НастройкиОтображения.ОтображатьОтмененные = Неопределено, Ложь, НастройкиОтображения.ОтображатьОтмененные));
	Запрос.УстановитьПараметр("ВыделенныеДаты", ВыделенныеДаты);
	// ирОбщий.ОтЛкс(Запрос) - для отладки запроса в точке останова
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаПоИсполнителю = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Сч = 0;
	Пока ВыборкаПоИсполнителю.Следующий() Цикл
		Если НЕ ИзмеренияЗаполнены Тогда
			Измерение = ИзмеренияИсполнители.Элементы.Добавить(ВыборкаПоИсполнителю.Исполнитель
			, ВыборкаПоИсполнителю.ИсполнительПредставление // для 8.3.16 закомментировать
			);
			Если Сч%2 = 0 Тогда
				Цвет = WebЦвета.Белый;
			Иначе
				Цвет = Новый Цвет(250,250,250);
			КонецЕсли;
			Измерение.ЦветФона = Цвет;
			Сч = Сч + 1;
		КонецЕсли;
		ЗначенияИзмерений = Новый Соответствие;
		ЗначенияИзмерений.Вставить("Исполнители",ВыборкаПоИсполнителю.Исполнитель);
		Выборка = ВыборкаПоИсполнителю.Выбрать();
		Пока Выборка.Следующий() Цикл
			ЭлементПланировщика = Планировщик.Элементы.Добавить(Выборка.ДатаНачала, Выборка.ДатаОкончания);
			Событие = ск_ОбщегоНазначенияСервер.ОбъектВСтруктуру(Выборка.Ссылка.ПолучитьОбъект()
			,Новый Структура("ИсполнительПредставление, Цвет, Состояние, Подсказка", 
				Выборка.ИсполнительПредставление, Выборка.ЗадачаЦветЗадачи, Выборка.Состояние, Выборка.Подсказка));
			ЭлементПланировщика.Значение = Событие;
			ЭлементПланировщика.ЗначенияИзмерений = Новый ФиксированноеСоответствие(ЗначенияИзмерений);
			ЭлементПланировщика.Подсказка = Выборка.Подсказка;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Функция ТекстЗапросаПолученияЭлементовРасписания()
	ТекстЗапроса = 
"//{Запрос: 0, -3 ////////////////////////////////////////
|ВЫБРАТЬ
|	ДОБАВИТЬКДАТЕ(&НачалоПериода, ДЕНЬ, aa.a * 1000 + bb.b * 100 + cc.c * 10 + dd.d) КАК Период
|ПОМЕСТИТЬ Даты
|ИЗ
|	(ВЫБРАТЬ
|		0 КАК a
|	ОБЪЕДИНИТЬ
|	ВЫБРАТЬ
|		1 КАК a
|	ОБЪЕДИНИТЬ
|	ВЫБРАТЬ
|		2 КАК a
|	ОБЪЕДИНИТЬ
|	ВЫБРАТЬ
|		3 КАК a
|	ОБЪЕДИНИТЬ
|	ВЫБРАТЬ
|		4 КАК a
|	ОБЪЕДИНИТЬ
|	ВЫБРАТЬ
|		5 КАК a
|	ОБЪЕДИНИТЬ
|	ВЫБРАТЬ
|		6 КАК a
|	ОБЪЕДИНИТЬ
|	ВЫБРАТЬ
|		7 КАК a
|	ОБЪЕДИНИТЬ
|	ВЫБРАТЬ
|		8 КАК a
|	ОБЪЕДИНИТЬ
|	ВЫБРАТЬ
|		9 КАК a) КАК aa
|	ПОЛНОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
|			0 КАК b
|		ОБЪЕДИНИТЬ
|		ВЫБРАТЬ
|			1 КАК b
|		ОБЪЕДИНИТЬ
|		ВЫБРАТЬ
|			2 КАК b
|		ОБЪЕДИНИТЬ
|		ВЫБРАТЬ
|			3 КАК b
|		ОБЪЕДИНИТЬ
|		ВЫБРАТЬ
|			4 КАК b
|		ОБЪЕДИНИТЬ
|		ВЫБРАТЬ
|			5 КАК b
|		ОБЪЕДИНИТЬ
|		ВЫБРАТЬ
|			6 КАК b
|		ОБЪЕДИНИТЬ
|		ВЫБРАТЬ
|			7 КАК b
|		ОБЪЕДИНИТЬ
|		ВЫБРАТЬ
|			8 КАК b
|		ОБЪЕДИНИТЬ
|		ВЫБРАТЬ
|			9 КАК b) КАК bb
|	ПО ИСТИНА
|	ПОЛНОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
|			0 КАК c
|		ОБЪЕДИНИТЬ
|		ВЫБРАТЬ
|			1 КАК c
|		ОБЪЕДИНИТЬ
|		ВЫБРАТЬ
|			2 КАК c
|		ОБЪЕДИНИТЬ
|		ВЫБРАТЬ
|			3 КАК c
|		ОБЪЕДИНИТЬ
|		ВЫБРАТЬ
|			4 КАК c
|		ОБЪЕДИНИТЬ
|		ВЫБРАТЬ
|			5 КАК c
|		ОБЪЕДИНИТЬ
|		ВЫБРАТЬ
|			6 КАК c
|		ОБЪЕДИНИТЬ
|		ВЫБРАТЬ
|			7 КАК c
|		ОБЪЕДИНИТЬ
|		ВЫБРАТЬ
|			8 КАК c
|		ОБЪЕДИНИТЬ
|		ВЫБРАТЬ
|			9 КАК c) КАК cc
|	ПО ИСТИНА
|	ПОЛНОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
|			0 КАК d
|		ОБЪЕДИНИТЬ
|		ВЫБРАТЬ
|			1 КАК d
|		ОБЪЕДИНИТЬ
|		ВЫБРАТЬ
|			2 КАК d
|		ОБЪЕДИНИТЬ
|		ВЫБРАТЬ
|			3 КАК d
|		ОБЪЕДИНИТЬ
|		ВЫБРАТЬ
|			4 КАК d
|		ОБЪЕДИНИТЬ
|		ВЫБРАТЬ
|			5 КАК d
|		ОБЪЕДИНИТЬ
|		ВЫБРАТЬ
|			6 КАК d
|		ОБЪЕДИНИТЬ
|		ВЫБРАТЬ
|			7 КАК d
|		ОБЪЕДИНИТЬ
|		ВЫБРАТЬ
|			8 КАК d
|		ОБЪЕДИНИТЬ
|		ВЫБРАТЬ
|			9 КАК d) КАК dd
|	ПО ИСТИНА
|ГДЕ aa.a * 1000 + bb.b * 100 + cc.c * 10 + dd.d <= РАЗНОСТЬДАТ(&НачалоПериода, &КонецПериода, ДЕНЬ)
|;
|//{Запрос: 1, -2 ////////////////////////////////////////
|ВЫБРАТЬ
|	Тт_ЭлементыРасписания_ФактТ.Ссылка КАК Ссылка
|ПОМЕСТИТЬ Открытые
|ИЗ
|	Справочник.тт_ЭлементыРасписания.Факт КАК Тт_ЭлементыРасписания_ФактТ
|ГДЕ ИСТИНА
|	И Тт_ЭлементыРасписания_ФактТ.ДатаОкончанияФакт = &ПустаяДата
|	И (ЛОЖЬ
|		ИЛИ &ВсеИсполнители
|		ИЛИ Тт_ЭлементыРасписания_ФактТ.Ссылка.Исполнитель В (&Исполнители))
|;
|//{Запрос: _1_ЭлементыРасписания ////////////////////////////////////////
|//{Выборка: Планы ////////////////////////////////////////
|ВЫБРАТЬ
|	тт_ЭлементыРасписанияТ.Ссылка КАК Ссылка,
|	тт_ЭлементыРасписанияТ.Задача КАК Задача,
|	тт_ЭлементыРасписанияТ.Задача.ТекстСодержания КАК Подсказка,
|	тт_ЭлементыРасписанияТ.Исполнитель КАК Исполнитель,
|	тт_ЭлементыРасписанияТ.Исполнитель.Представление КАК ИсполнительПредставление,
|	тт_ЭлементыРасписанияТ.Автор КАК Автор,
|	ВЫБОР
|		КОГДА тт_ЭлементыРасписанияТ.ДатаНачалаПлан < Даты.Период
|			ТОГДА Даты.Период
|		ИНАЧЕ тт_ЭлементыРасписанияТ.ДатаНачалаПлан
|	КОНЕЦ КАК ДатаНачала,
|	ВЫБОР
|		КОГДА тт_ЭлементыРасписанияТ.ДатаОкончанияПлан > КОНЕЦПЕРИОДА(Даты.Период, ДЕНЬ)
|			ТОГДА КОНЕЦПЕРИОДА(Даты.Период, ДЕНЬ)
|		ИНАЧЕ тт_ЭлементыРасписанияТ.ДатаОкончанияПлан
|	КОНЕЦ КАК ДатаОкончания,
|	Даты.Период КАК Период,
|	тт_ЭлементыРасписанияТ.Задача.ЦветЗадачи КАК ЗадачаЦветЗадачи,
|	""План"" КАК Состояние
|ИЗ
|	Справочник.тт_ЭлементыРасписания КАК тт_ЭлементыРасписанияТ
|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Даты КАК Даты
|	ПО ИСТИНА
|		И тт_ЭлементыРасписанияТ.ДатаНачалаПлан <= КОНЕЦПЕРИОДА(Даты.Период, День)
|		И тт_ЭлементыРасписанияТ.ДатаОкончанияПлан >= Даты.Период
|	ЛЕВОЕ СОЕДИНЕНИЕ Открытые КАК Открытые
|	ПО тт_ЭлементыРасписанияТ.Ссылка = Открытые.Ссылка
|ГДЕ ИСТИНА
|	И (ЛОЖЬ
|		ИЛИ &ВсеИсполнители
|		ИЛИ тт_ЭлементыРасписанияТ.Исполнитель В (&Исполнители))
|	И (ЛОЖЬ
|		ИЛИ тт_ЭлементыРасписанияТ.ПометкаУдаления = ЛОЖЬ
|		ИЛИ &ОтображатьОтмененные)
|	И Открытые.Ссылка ЕСТЬ NULL
|ОБЪЕДИНИТЬ ВСЕ
|//{Выборка: Текущие ////////////////////////////////////////
|ВЫБРАТЬ
|	тт_ЭлементыРасписанияТ.Ссылка КАК Ссылка,
|	тт_ЭлементыРасписанияТ.Задача КАК Задача,
|	тт_ЭлементыРасписанияТ.Задача.ТекстСодержания КАК Подсказка,
|	тт_ЭлементыРасписанияТ.Исполнитель КАК Исполнитель,
|	тт_ЭлементыРасписанияТ.Исполнитель.Представление КАК ИсполнительПредставление,
|	тт_ЭлементыРасписанияТ.Автор КАК Автор,
|	ВЫБОР
|		КОГДА тт_ЭлементыРасписанияТ.ДатаНачалаПлан < Даты.Период
|			ТОГДА Даты.Период
|		ИНАЧЕ тт_ЭлементыРасписанияТ.ДатаНачалаПлан
|	КОНЕЦ КАК ДатаНачала,
|	ВЫБОР
|		КОГДА тт_ЭлементыРасписанияТ.ДатаОкончанияПлан > КОНЕЦПЕРИОДА(Даты.Период, ДЕНЬ)
|			ТОГДА КОНЕЦПЕРИОДА(Даты.Период, ДЕНЬ)
|		ИНАЧЕ тт_ЭлементыРасписанияТ.ДатаОкончанияПлан
|	КОНЕЦ КАК ДатаОкончания,
|	Даты.Период КАК Период,
|	тт_ЭлементыРасписанияТ.Задача.ЦветЗадачи КАК ЗадачаЦветЗадачи,
|	""Текущий"" КАК Состояние
|ИЗ
|	Справочник.тт_ЭлементыРасписания КАК тт_ЭлементыРасписанияТ
|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Даты КАК Даты
|	ПО ИСТИНА
|		И тт_ЭлементыРасписанияТ.ДатаНачалаПлан <= КОНЕЦПЕРИОДА(Даты.Период, День)
|		И тт_ЭлементыРасписанияТ.ДатаОкончанияПлан >= Даты.Период
|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Открытые КАК Открытые
|	ПО тт_ЭлементыРасписанияТ.Ссылка = Открытые.Ссылка
|ГДЕ ИСТИНА
|	И (ЛОЖЬ
|		ИЛИ &ВсеИсполнители
|		ИЛИ тт_ЭлементыРасписанияТ.Исполнитель В (&Исполнители))
|	И (ЛОЖЬ
|		ИЛИ тт_ЭлементыРасписанияТ.ПометкаУдаления = ЛОЖЬ
|		ИЛИ &ОтображатьОтмененные)
|ОБЪЕДИНИТЬ ВСЕ
|//{Выборка: Факты ////////////////////////////////////////
|ВЫБРАТЬ
|	Тт_ЭлементыРасписания_ФактТ.Ссылка КАК Ссылка,
|	Тт_ЭлементыРасписания_ФактТ.Ссылка.Задача КАК Задача,
|	Тт_ЭлементыРасписания_ФактТ.Ссылка.Задача.ТекстСодержания КАК Подсказка,
|	Тт_ЭлементыРасписания_ФактТ.Ссылка.Исполнитель КАК Исполнитель,
|	Тт_ЭлементыРасписания_ФактТ.Ссылка.Исполнитель.Представление КАК ИсполнительПредставление,
|	Тт_ЭлементыРасписания_ФактТ.Ссылка.Автор КАК Автор,
|	ВЫБОР
|		КОГДА Тт_ЭлементыРасписания_ФактТ.ДатаНачалаФакт < Даты.Период
|			ТОГДА Даты.Период
|		ИНАЧЕ Тт_ЭлементыРасписания_ФактТ.ДатаНачалаФакт
|	КОНЕЦ КАК ДатаНачала,
|	ВЫБОР
|		КОГДА Тт_ЭлементыРасписания_ФактТ.ДатаОкончанияФакт > КОНЕЦПЕРИОДА(Даты.Период, ДЕНЬ)
|			ТОГДА КОНЕЦПЕРИОДА(Даты.Период, ДЕНЬ)
|		ИНАЧЕ Тт_ЭлементыРасписания_ФактТ.ДатаОкончанияФакт
|	КОНЕЦ КАК ДатаОкончания,
|	Даты.Период КАК Период,
|	Тт_ЭлементыРасписания_ФактТ.Ссылка.Задача.ЦветЗадачи КАК ЗадачаЦветЗадачи,
|	""Факт"" КАК Состояние
|ИЗ
|	Справочник.тт_ЭлементыРасписания.Факт КАК Тт_ЭлементыРасписания_ФактТ
|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Даты КАК Даты
|	ПО ИСТИНА
|		И Тт_ЭлементыРасписания_ФактТ.ДатаНачалаФакт <= КОНЕЦПЕРИОДА(Даты.Период, День)
|		И Тт_ЭлементыРасписания_ФактТ.ДатаОкончанияФакт >= Даты.Период
|ГДЕ ИСТИНА
|	И (ЛОЖЬ
|		ИЛИ &ВсеИсполнители
|		ИЛИ Тт_ЭлементыРасписания_ФактТ.Ссылка.Исполнитель В (&Исполнители))
|	И (ЛОЖЬ
|		ИЛИ Тт_ЭлементыРасписания_ФактТ.Ссылка.ПометкаУдаления = ЛОЖЬ
|		ИЛИ &ОтображатьОтмененные)
|ОБЪЕДИНИТЬ ВСЕ
|//{Выборка: Открытые ////////////////////////////////////////
|ВЫБРАТЬ
|	Тт_ЭлементыРасписания_ФактТ.Ссылка КАК Ссылка,
|	Тт_ЭлементыРасписания_ФактТ.Ссылка.Задача КАК Задача,
|	Тт_ЭлементыРасписания_ФактТ.Ссылка.Задача.ТекстСодержания КАК Подсказка,
|	Тт_ЭлементыРасписания_ФактТ.Ссылка.Исполнитель КАК Исполнитель,
|	Тт_ЭлементыРасписания_ФактТ.Ссылка.Исполнитель.Представление КАК ИсполнительПредставление,
|	Тт_ЭлементыРасписания_ФактТ.Ссылка.Автор КАК Автор,
|	ВЫБОР
|		КОГДА Тт_ЭлементыРасписания_ФактТ.ДатаНачалаФакт < Даты.Период
|			ТОГДА Даты.Период
|		ИНАЧЕ Тт_ЭлементыРасписания_ФактТ.ДатаНачалаФакт
|	КОНЕЦ КАК ДатаНачала,
|	ВЫБОР
|		КОГДА &Сейчас > КОНЕЦПЕРИОДА(Даты.Период, ДЕНЬ)
|			ТОГДА КОНЕЦПЕРИОДА(Даты.Период, ДЕНЬ)
|		ИНАЧЕ &Сейчас
|	КОНЕЦ КАК ДатаОкончания,
|	Даты.Период КАК Период,
|	Тт_ЭлементыРасписания_ФактТ.Ссылка.Задача.ЦветЗадачи КАК ЗадачаЦветЗадачи,
|	""Открыт"" КАК Состояние
|ИЗ
|	Справочник.тт_ЭлементыРасписания.Факт КАК Тт_ЭлементыРасписания_ФактТ
|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Даты КАК Даты
|	ПО ИСТИНА
|		И Тт_ЭлементыРасписания_ФактТ.ДатаНачалаФакт <= КОНЕЦПЕРИОДА(Даты.Период, День)
|		И Тт_ЭлементыРасписания_ФактТ.ДатаОкончанияФакт = &ПустаяДата
|		И КОНЕЦПЕРИОДА(&Сейчас, ДЕНЬ) >= Даты.Период
|ГДЕ ИСТИНА
|	И (ЛОЖЬ
|		ИЛИ &ВсеИсполнители
|		ИЛИ Тт_ЭлементыРасписания_ФактТ.Ссылка.Исполнитель В (&Исполнители))
|	И (ЛОЖЬ
|		ИЛИ Тт_ЭлементыРасписания_ФактТ.Ссылка.ПометкаУдаления = ЛОЖЬ
|		ИЛИ &ОтображатьОтмененные)
|ИТОГИ
|ПО
|	Исполнитель
|";
	Возврат ТекстЗапроса;
КонецФункции

