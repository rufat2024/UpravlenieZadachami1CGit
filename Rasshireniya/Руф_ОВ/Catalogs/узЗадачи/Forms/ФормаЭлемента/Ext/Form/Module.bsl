
&НаСервере
Процедура Руф_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	ДополнитьЗапросДинСпискаФактЧасы();
	ДобавлениеЭлементовВФорму(); 
	
	//+++Руф
	Элементы.ФактическиеЧасыКомандаДобавитьФактРабот.Доступность = Не Параметры.Ключ.Пустая();
	//+++Руф
	
КонецПроцедуры

&НаСервере
Процедура ДополнитьЗапросДинСпискаФактЧасы()

	ТекстрДо = "РегистрНакопленияузФактПоЗадачам.Примечание"; 
	
	ТекстрПосле = 	"РегистрНакопленияузФактПоЗадачам.Примечание,
					|	РегистрНакопленияузФактПоЗадачам.Руф_ТипВремени,
					|	РегистрНакопленияузФактПоЗадачам.Руф_ВРасчет";
	
	ФактическиеЧасы.ТекстЗапроса = СтрЗаменить(ФактическиеЧасы.ТекстЗапроса, ТекстрДо, ТекстрПосле);

КонецПроцедуры

&НаСервере
Функция ДобавлениеЭлементовВФорму()
	
	//(Руф добавление новых элементов в форму
	ЭлементРуфДоработки = ЭтаФорма.Элементы.Добавить("РуфДоработки", Тип("ГруппаФормы"),Элементы.Группа7);
	ЭлементРуфДоработки.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ЭлементРуфДоработки.Отображение = ОтображениеОбычнойГруппы.СильноеВыделение;
	ЭлементРуфДоработки.ОтображатьЗаголовок = Истина;
	ЭлементРуфДоработки.Заголовок = "Доработки (Руф)";
	ЭлементРуфДоработки.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	НовыйЭлемент = ЭтаФорма.Элементы.Добавить("Руф_Постановщик", Тип("ПолеФормы"),Элементы.РуфДоработки);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Объект.Руф_Постановщик";  
	
	НовыйЭлемент = ЭтаФорма.Элементы.Добавить("Руф_Наряд", Тип("ПолеФормы"),Элементы.РуфДоработки);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Объект.Руф_Наряд"; 
	
	НовыйЭлемент = ЭтаФорма.Элементы.Добавить("Руф_ТипВремени", Тип("ПолеФормы"),Элементы.РуфДоработки);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Объект.Руф_ТипВремени";     
	
	НовыйЭлемент = ЭтаФорма.Элементы.Добавить("Руф_ВЗР", Тип("ПолеФормы"),Элементы.РуфДоработки);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Объект.Руф_ВЗР";   
	
	НовыйЭлемент = ЭтаФорма.Элементы.Добавить("Руф_НомерФорумаВнешнейЗаявки", Тип("ПолеФормы"),Элементы.РуфДоработки);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Объект.Руф_НомерФорумаВнешнейЗаявки";
	//)Руф добавление новых элементов в форму
	
	//( Руф Открытие сайта б24
	Группа_ВнешнуюЗаявку = ЭтаФорма.Элементы.Вставить("Группа_ВнешнуюЗаявку", Тип("ГруппаФормы"),Элементы.Группа7,Элементы.НомерЗаявки);
	Группа_ВнешнуюЗаявку.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	Группа_ВнешнуюЗаявку.Отображение = ОтображениеОбычнойГруппы.Нет;
	Группа_ВнешнуюЗаявку.ОтображатьЗаголовок = Ложь;
	Группа_ВнешнуюЗаявку.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;

	Элементы.URLЗаявки.Видимость = Ложь;
	
	Руф_URLЗаявки = ЭтаФорма.Элементы.Добавить("Руф_URLЗаявки", Тип("ПолеФормы"),Элементы.Группа_ВнешнуюЗаявку);
	Руф_URLЗаявки.Вид = ВидПоляФормы.ПолеВвода;
	Руф_URLЗаявки.ПутьКДанным = "Объект.URLВнешнейЗаявки";
	
	Команда_ОткрытьВнешнуюЗаявку = ЭтаФорма.Команды.Добавить("Команда_ОткрытьВнешнуюЗаявку");
	Команда_ОткрытьВнешнуюЗаявку.Заголовок = "Открыть";
	Команда_ОткрытьВнешнуюЗаявку.Действие = "Команда_ОткрытьВнешнуюЗаявку"; //указываем только имя процедуры 
	
	Кнопка_ОткрытьВнешнуюЗаявку = ЭтаФорма.Элементы.Добавить("Кнопка_ОткрытьВнешнуюЗаявку", Тип("КнопкаФормы"),Элементы.Группа_ВнешнуюЗаявку);
	Кнопка_ОткрытьВнешнуюЗаявку.Заголовок = "Открыть";
	Кнопка_ОткрытьВнешнуюЗаявку.ИмяКоманды = "Команда_ОткрытьВнешнуюЗаявку"; 
	
	//) Руф Открытие сайта б24
	
	//( Руф Добавим колонки в факт время
	
	//Создаем колонку для выбора и ввода
	НовыйЭлемент  = Элементы.Вставить("ФактическиеЧасыРуф_ТипВремени",Тип("ПолеФормы"),Элементы.ФактическиеЧасы, Элементы.ФактическиеЧасыДатаНачала);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;      
	НовыйЭлемент.ПутьКДанным = "ФактическиеЧасы.Руф_ТипВремени";  
	
	НовыйЭлемент  = Элементы.Вставить("ФактическиеЧасыРуф_ВРасчет",Тип("ПолеФормы"),Элементы.ФактическиеЧасы, Элементы.ФактическиеЧасыФакт);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;      
	НовыйЭлемент.ПутьКДанным = "ФактическиеЧасы.Руф_ВРасчет";
	
	//) Руф Добавим колонки в факт время
	
	
КонецФункции   

&НаКлиенте
Процедура Команда_ОткрытьВнешнуюЗаявку(Команда)

	Если Не Объект.URLВнешнейЗаявки = "" Тогда
		ЗапуститьПриложение(Объект.URLВнешнейЗаявки);
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("УстановитьВидимостьДоступность")
Процедура Руф_УстановитьВидимостьДоступность()

	Элементы.Родитель.АвтоОтметкаНезаполненного = ТребоватьЗаполнитьРодителя;
	Элементы.Родитель.АвтоВыборНезаполненного = ТребоватьЗаполнитьРодителя;	
	Элементы.ГруппаСтраницаИзмененныеОбъектыДетали.Видимость = Ложь;
	Элементы.ГруппаСтраницаСписокИзмененныхОбъектов.Видимость = Ложь;
	Элементы.ГруппаКомментарии.Видимость = Ложь;
	Элементы.КомментарииДобавить.Видимость = Ложь;
	Элементы.КомментарииВывестиСписок.Видимость = Ложь;
	Элементы.КомментарииПереместитьВверх.Видимость = Ложь;
	Элементы.КомментарииПереместитьВниз.Видимость = Ложь; 
	//+ #104 Дзеса Ігор (capitoshko) 09.10.2018
	//Элементы.ЧасыФакт.ТолькоПросмотр = Истина;
	//- #104 Дзеса Ігор (capitoshko) 09.10.2018 
	Элементы.ГруппаСтраницаФорматированныйТекст.Видимость = Ложь;
	Элементы.ГруппаСтраницаПросмотр.Видимость = Ложь;
	Элементы.ГруппаСтраницаТекст.Видимость = Ложь;
	Элементы.ГруппаСтраницаКод.Видимость = Ложь;
	Элементы.ГруппаКоманднаяПанельMarkdown.Видимость = Ложь;

#Удаление
	Элементы.ГруппаСтраницаВопросыИОтветы.Видимость = Ложь;
	Элементы.ГруппаСтраницаДополнительно.Видимость = Ложь;
	Элементы.ГруппаСтраницаНастройкиОсновнаяЗадача.Видимость = Ложь;
	Элементы.ГруппаСтраницаУчетВремени.Видимость = Ложь;
	Элементы.ГруппаСтраницаИстория.Видимость = Ложь;
	Элементы.ГруппаСтраницаИзмененныеОбъекты.Видимость = Ложь;
#КонецУдаления
#Вставка
// Здесь можно описать новое поведение.
	//Элементы.ГруппаСтраницаВопросыИОтветы.Видимость = Ложь;
	//Элементы.ГруппаСтраницаДополнительно.Видимость = Ложь;
	//Элементы.ГруппаСтраницаНастройкиОсновнаяЗадача.Видимость = Ложь;
	//Элементы.ГруппаСтраницаУчетВремени.Видимость = Ложь;
	//Элементы.ГруппаСтраницаИстория.Видимость = Ложь;
	//Элементы.ГруппаСтраницаИзмененныеОбъекты.Видимость = Ложь;

#КонецВставки

	//+ #287 Пихоцкий Юрий (pihy86) 01.06.2021
	Элементы.ГруппаСтраницаТаблица.Видимость = Ложь;
	//- #287 Пихоцкий Юрий (pihy86) 01.06.2021
	// +SZ #277 16.01.2021
	Элементы.Содержание.ТолькоПросмотр = Истина;
	// -SZ #277 16.01.2021  
	Элементы.ПроектЗадачи.Видимость = Ложь;  
	Элементы.ОсновнаяЗадачаПроект.Видимость = Ложь;

	Если ЗначениеЗаполнено(Объект.ОсновнаяЗадача) Тогда  
		Элементы.ОсновнаяЗадачаПроект.Видимость = Истина;
	Иначе
		Элементы.ПроектЗадачи.Видимость = Истина;
	КонецЕсли;

	// [+] #285 Павлюков С.Ю. 2024-04-14
	ОбновитьДоступныйСписокСменыСтатуса();
	// [-] #285 Павлюков С.Ю. 2024-04-14

	Если Объект.ОформлениеТекста = ПредопределенноеЗначение("Перечисление.узОформлениеТекста.ФорматированныйТекст") Тогда
		Элементы.ГруппаСтраницаФорматированныйТекст.Видимость = Истина;
	ИначеЕсли Объект.ОформлениеТекста = ПредопределенноеЗначение("Перечисление.узОформлениеТекста.Markdown") Тогда
		Элементы.ГруппаКоманднаяПанельMarkdown.Видимость = Истина;
		Элементы.ГруппаСтраницаПросмотр.Видимость = Истина;
		Элементы.ГруппаСтраницаТекст.Видимость = Истина;
	Иначе
		Элементы.ГруппаСтраницаТекст.Видимость = Истина;
	Конецесли;

	Если Объект.ПоказыватьКод Тогда
		Элементы.ГруппаСтраницаКод.Видимость = Истина;
	КонецЕсли;

	Если Объект.ПоказыватьТаблицу Тогда
		Элементы.ГруппаСтраницаТаблица.Видимость = Истина;
	КонецЕсли;	

	Если ТолькоСписокИзмененныхОбъектов Тогда
		Элементы.ГруппаСтраницаСписокИзмененныхОбъектов.Видимость = Истина;	
	Иначе
		Элементы.ГруппаСтраницаИзмененныеОбъектыДетали.Видимость = Истина;
	Конецесли;
	Элементы.ГруппаКомментарии.Видимость = Истина;	
	Элементы.КомментарииДобавить.Видимость = Истина;
	Элементы.КомментарииВывестиСписок.Видимость = Истина;		
	Элементы.КомментарииПереместитьВверх.Видимость = Истина;
	Элементы.КомментарииПереместитьВниз.Видимость = Истина;		

	//Если Объект.ЭтоОсновнаяЗадача Тогда
	//	Элементы.ГруппаСтраницаНастройкиОсновнаяЗадача.Видимость = Истина;
	//Конецесли;

	Элементы.ДекорацияИнформацияОСлежениеЗаЗадачей.Видимость = Ложь;
	пЕстьЛиСлежение = РегистрыСведений.узНаблюдателиЗаЗадачами.ЕстьЛиСлежениеЗаЗадачейУТекущегоПользователя(Объект.Ссылка);
	Если пЕстьЛиСлежение Тогда
		Элементы.ДекорацияИнформацияОСлежениеЗаЗадачей.Видимость = Истина;
	Конецесли;

	// +SZ #277 16.01.2021
	Если Объект.Ссылка.Пустая() Тогда

		Элементы.Содержание.ТолькоПросмотр = Ложь;

	Иначе

		пТолькоПросмотрДляСодержанияЗадач = Справочники.узКонстанты.ПолучитьЗначениеКонстанты(
		"ТолькоПросмотрДляСодержанияЗадач", Тип("Булево"),,, Ложь);

		Если пТолькоПросмотрДляСодержанияЗадач = Неопределено Тогда
			Элементы.Содержание.ТолькоПросмотр = Ложь;
		Иначе
			Элементы.Содержание.ТолькоПросмотр = пТолькоПросмотрДляСодержанияЗадач;
		КонецЕсли;

	КонецЕсли;
	// -SZ #277 16.01.2021

	ВТДопПараметры = Новый Структура();
	//+ #104 Дзеса Ігор (capitoshko) 08.10.2018
	//ВТДопПараметры.Вставить("ФактическиеЧасы_Количество",Объект.ФактическиеЧасы.Количество());		
	//ВидимостьДоступность = ПолучитьВидимостьДоступностьЭлементов(ВТДопПараметры);

	//Элементы.ЧасыФакт.ТолькоПросмотр = ВидимостьДоступность.ЧасыФакт_ТолькоПросмотр;
	//- #104 Дзеса Ігор (capitoshko) 08.10.2018 

	ОбновитьЗаголовокПоказатьСкрытьКомментарии(); 

	ОбновитьВидимостьКомандаМетки();

КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("ЗаполнитьЗначенияИзРодителя")
Процедура Руф_ЗаполнитьЗначенияИзРодителя()
	МассивРеквизитов = Новый Массив();
	МассивРеквизитов.Добавить("Контрагент");
	МассивРеквизитов.Добавить("Конфигурация");
	//+ #201 Иванов А.Б. 2020-05-23 Изменения от Дениса Урянского @d-hurricane
	//МассивРеквизитов.Добавить("ГруппаДоступаЗадач");//Павлюков
	МассивРеквизитов.Добавить("ГруппаДоступа");
#Удаление
	//- #201 Иванов А.Б. 2020-05-23 Изменения от Дениса Урянского @d-hurricane
#КонецУдаления
#Вставка
// Здесь можно описать новое поведение.
	//- #201 Иванов А.Б. 2020-05-23 Изменения от Дениса Урянского @d-hurricane     
	
	МассивРеквизитов.Добавить("Руф_Постановщик");    
	МассивРеквизитов.Добавить("Руф_ТипВремени");
	МассивРеквизитов.Добавить("Руф_ВЗР");
	//МассивРеквизитов.Добавить("Руф_Постановщик");

#КонецВставки

	Для каждого ИмяРеквизита из МассивРеквизитов цикл
		ЗначениеРеквизита = Объект[ИмяРеквизита];
		Если ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
			Продолжить;
		Конецесли;	
		ЗначениеРеквизитаРодителя = Объект.Родитель[ИмяРеквизита];
		Если НЕ ЗначениеЗаполнено(ЗначениеРеквизитаРодителя) Тогда
			Продолжить;
		Конецесли;		

		Объект[ИмяРеквизита] = ЗначениеРеквизитаРодителя;	
	Конеццикла;
КонецПроцедуры

&НаКлиенте
Процедура Руф_ПослеЗаписиПосле(ПараметрыЗаписи) 
	
	//+++Руф
	Элементы.ФактическиеЧасыКомандаДобавитьФактРабот.Доступность = Истина; 
	//+++Руф      
	
КонецПроцедуры



