
&ИзменениеИКонтроль("ПолучитьЗначениеКонстанты")
Функция Руф_ПолучитьЗначениеКонстанты(ИмяКонстанты, ТипКонстанты, Отказ, ВыдаватьСообщенияОбОшибках, ВыдаватьИсключения)
	Перем мЗначениеКонстанты;

	МассивОшибок = Новый Массив();

#Удаление
	пКонстанта = ПредопределенноеЗначение("Справочник.узКонстанты."+ИмяКонстанты);
#КонецУдаления
#Вставка
// Здесь можно описать новое поведение.
	пКонстанта = ПредопределенноеЗначение("Справочник.узКонстанты."+ИмяКонстанты);
	
	//+rufat@, 2025-03-14 02:10:07 Учет времени ОВ
	Если Не ЗначениеЗаполнено(пКонстанта) Тогда
		
		пКонстанта = Справочники.узКонстанты.НайтиПоНаименованию(ИмяКонстанты);	
	КонецЕсли;
	//+/rufat@, 2025-03-14 02:10:07

#КонецВставки

	мЗначениеКонстанты = пКонстанта.Значение;	

	Если ТипЗнч(ТипКонстанты) = Тип("Строка") Тогда
		ТипКонстанты = Тип(ТипКонстанты);
	Конецесли;

	Если мЗначениеКонстанты = Неопределено 
		ИЛИ ТипЗнч(мЗначениеКонстанты) <> ТипКонстанты
		ИЛИ НЕ ЗначениеЗаполнено(мЗначениеКонстанты) Тогда

		МассивОшибок.Добавить("Ошибка! Не указано значение элемента """+ИмяКонстанты+""", в константах");
		Отказ = Истина;
	Конецесли;

	Если ВыдаватьСообщенияОбОшибках Тогда
		Для каждого ЭлМассиваОшибок из МассивОшибок цикл
			Сообщить(ЭлМассиваОшибок);
		Конеццикла;	
	Конецесли;

	Если Отказ 
		И ВыдаватьИсключения Тогда
		ВызватьИсключение "Ошибка! Не заполнена константа " + ИмяКонстанты;
	Конецесли;

	Возврат мЗначениеКонстанты;
КонецФункции

&ИзменениеИКонтроль("УстановитьЗначениеКонстанты")
Процедура Руф_УстановитьЗначениеКонстанты(ИмяКонстанты, ЗначениеКонстанты)

#Удаление
	пКонстанта = ПредопределенноеЗначение("Справочник.узКонстанты."+ИмяКонстанты);	
#КонецУдаления
#Вставка
// Здесь можно описать новое поведение.
	пКонстанта = ПредопределенноеЗначение("Справочник.узКонстанты."+ИмяКонстанты);	
   	//+rufat@, 2025-03-14 02:10:07 Учет времени ОВ
	Если Не ЗначениеЗаполнено(пКонстанта) Тогда
		
		пКонстанта = Справочники.узКонстанты.НайтиПоНаименованию(ИмяКонстанты);	
	КонецЕсли;
	//+/rufat@, 2025-03-14 02:10:07
#КонецВставки
	пКонстантаОбъект = пКонстанта.ПолучитьОбъект();
	пКонстантаОбъект.Значение = ЗначениеКонстанты;
	пКонстантаОбъект.Записать();

КонецПроцедуры
