#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Функция СтартРаботы(Ссылка) Экспорт
	 Объект = Ссылка.ПолучитьОбъект();
	 СтрокаФакта = Объект.Факт.Добавить();
	 СтрокаФакта.ДатаНачалаФакт = ТекущаяДатаСеанса();
	 СтрокаФакта.Примечание = Объект.Примечание;
	 Объект.Записать();
	 Возврат Истина;
КонецФункции

Функция ПаузаРаботы(Исполнитель) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	тт_ЭлементыРасписанияФакт.Ссылка КАК Ссылка,
		|	тт_ЭлементыРасписанияФакт.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	Справочник.тт_ЭлементыРасписания.Факт КАК тт_ЭлементыРасписанияФакт
		|ГДЕ
		|	тт_ЭлементыРасписанияФакт.Ссылка.Исполнитель = &Исполнитель
		|	И тт_ЭлементыРасписанияФакт.ДатаОкончанияФакт = &ПустаяДата
		|ИТОГИ
		|	КОЛИЧЕСТВО(НомерСтроки)
		|ПО
		|	Ссылка";
	
	Запрос.УстановитьПараметр("Исполнитель", Исполнитель);
	Запрос.УстановитьПараметр("ПустаяДата", Дата(1,1,1));
	
	РезультатЗапроса = Запрос.Выполнить();
	ЕстьИзменения = Ложь;
	ВыборкаСсылка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаСсылка.Следующий() Цикл
		Объект = ВыборкаСсылка.Ссылка.ПолучитьОбъект();
		Выборка = ВыборкаСсылка.Выбрать();
		Пока Выборка.Следующий() Цикл
			Объект.Факт[Выборка.НомерСтроки - 1].ДатаОкончанияФакт = ТекущаяДатаСеанса();
		КонецЦикла;
		Объект.Записать();
		ЕстьИзменения = Истина;
	КонецЦикла;
	
	Возврат ЕстьИзменения;
	
КонецФункции

#КонецЕсли