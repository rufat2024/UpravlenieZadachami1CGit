&НаСервере
Процедура Руф_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка) 
	
	//+rufat@, 2025-03-11 05:31:31 Учет времени ОВ
	Руф_ДобавлениеЭлементовВФорму();
	//+/rufat@, 2025-03-11 05:31:31
	
	//+rufat@, 2025-03-11 05:37:20 Учет времени ОВ - Группа дополнительно развернута при открытии
	Элементы.ГруппаДополнительно.Показать();
	//+/rufat@, 2025-03-11 05:37:20
	
	//+rufat@, 2025-03-11 05:51:22 Учет времени ОВ
	Если Параметры.Свойство("Задача") Тогда 
		ЭтаФорма.Руф_ТипВремени = Параметры.Задача.Руф_ТипВремени;
	КонецЕсли;
	//+/rufat@, 2025-03-11 05:51:22
		
	
КонецПроцедуры

&НаСервере
Процедура Руф_ДобавлениеЭлементовВФорму()
	
	//+rufat@, 2025-03-11 05:31:26 Учет времени ОВ - Команда 20 минут 
	Команда_Команда20Минут = ЭтаФорма.Команды.Добавить("Команда_Команда20Минут");
	Команда_Команда20Минут.Заголовок = "20";
	Команда_Команда20Минут.Действие = "Руф_Команда20Минут"; //указываем только имя процедуры	
	
	Кнопка_Команда20Минут = ЭтаФорма.Элементы.Вставить("Кнопка_Команда20Минут", Тип("КнопкаФормы"),Элементы.Группа5,Элементы.Команда30Минут);
	Кнопка_Команда20Минут.Заголовок = "20";
	Кнопка_Команда20Минут.ИмяКоманды = "Команда_Команда20Минут";
	Кнопка_Команда20Минут.Картинка = БиблиотекаКартинок.РегламентноеЗадание; 
	Кнопка_Команда20Минут.Отображение = ОтображениеКнопки.КартинкаИТекст;
	//+/rufat@, 2025-03-11 05:31:26    
	
	//+rufat@, 2025-03-11 05:46:01 Учет времени ОВ
	ДобавляемыеРеквизиты = Новый Массив; //Определяем массив добавляемых реквизитов
	
	НовыйРеквизит = Новый РеквизитФормы("Руф_ТипВремени",Новый ОписаниеТипов("СправочникСсылка.Руф_ТипыВремени")); 
	ДобавляемыеРеквизиты.Добавить(НовыйРеквизит); 
	
	НовыйРеквизит = Новый РеквизитФормы("Руф_ВРасчет",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15,6))); 
	ДобавляемыеРеквизиты.Добавить(НовыйРеквизит); 
	
	ИзменитьРеквизиты(ДобавляемыеРеквизиты); //Применяем реквизиты
	
	НовыйЭлемент = ЭтаФорма.Элементы.Добавить("Руф_ТипВремени", Тип("ПолеФормы"),Элементы.ГруппаШапкаЛевая);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Руф_ТипВремени";    
	
	НовыйЭлемент = ЭтаФорма.Элементы.Вставить("Руф_ВРасчет", Тип("ПолеФормы"),Элементы.Группа6,Элементы.Задача);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Руф_ВРасчет";
	
	//+/rufat@, 2025-03-11 05:46:01
	
КонецПроцедуры 

&НаКлиенте
Процедура Руф_Команда20Минут(Команда)
	УказатьФактВМинутах(20);
КонецПроцедуры

&НаКлиенте
&ИзменениеИКонтроль("УказатьФактВМинутах")
Процедура Руф_УказатьФактВМинутах(КоличествоМинут)
	#Удаление
	Факт = КоличествоМинут / 60;
	#КонецУдаления
	#Вставка
	Факт = КоличествоМинут / 60;
	
	Руф_ВРасчет = Факт;
	
	#КонецВставки
	
	ФактПриИзменении(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура Руф_ФактПриИзмененииПосле(Элемент)
	//+rufat@, 2025-03-11 05:50:08 Учет времени ОВ
	Руф_ВРасчет = Факт;
	//+/rufat@, 2025-03-11 05:50:08
КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("ЗаполнитьРеквизитыЗадачиНаСервере")
Процедура Руф_ЗаполнитьРеквизитыЗадачиНаСервере()

	СтрокаДанных = Объект.Данные.Добавить();
	СтрокаДанных.Спринт = Спринт;
	СтрокаДанных.ДатаНачала = ДатаНачала;
	СтрокаДанных.ДатаОкончания = ДатаОкончания;
	СтрокаДанных.Факт = Факт;
#Удаление
	СтрокаДанных.Примечание = Примечание;
#КонецУдаления
#Вставка
	СтрокаДанных.Примечание = Примечание;  
	
	СтрокаДанных.Руф_ВРасчет = ЭтаФорма.Руф_ВРасчет;
	СтрокаДанных.Руф_ТипВремени = ЭтаФорма.Руф_ТипВремени;

#КонецВставки

КонецПроцедуры




                                                                                                                                                                              