
#Область СозданиеБазы

// Функция - Выполнить команду создать файловую базу
//
// Параметры:
//  Данные	 - Структура - Данные выполнения для команды
//		Приложение1с - Строка - Путь к 1с	
//		Путь - Строка - Каталог создания базы	
// 
// Возвращаемое значение:
//  Результат - Число
//		0 - Успешно
//		Другое число - Ошибка
//"C:\Program Files (x86)\1cv8\common\1cestart.exe" CREATEINFOBASE File=C:\v8_temp;
Функция ВыполнитьКомандуСоздатьФайловуюБазу(Данные) Экспорт 
	
	Результат = 999;
	
	ПутьТемп		= ПолучитьПутьКВременнойБазе(Данные.Путь);	
	ТекстКоманды	= СтрШаблон("""%1"" CREATEINFOBASE File=%2", Данные.Приложение1с, ПутьТемп);
	
	ЗапуститьПриложение(ТекстКоманды,, Истина, Результат);
	
	Возврат Результат
	
КонецФункции

Процедура УдалитьФайловуюБазу(Путь) Экспорт 
	
	Файл = Новый Файл(Путь);
	
	Если Файл.Существует() Тогда 
		УдалитьФайлы(Путь);
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьПутьКВременнойБазе(Путь) Экспорт 
	Возврат СтрШаблон("%1\v8_temp", Путь); 
КонецФункции

#КонецОбласти

#Область Хранилище

// Процедура - Выгрузить конфигурацию в файл
//
// Параметры:
//  Данные	 - Структура - Данные выполнения для команды
//		Приложение1с - Строка - Путь к 1с	
//		Путь - Строка - Каталог создания базы	
//		КаталогХранилища - Строка - Путь к хранилищу	
//		ПользовательХранилища - Строка - пользователь хранилища	
//		ПарольПользователяВХранилище - Строка - пароль пользователя хранилища	
//		ПутьФайла - Строка - Путь результата выгрузки	
//  Отказ	 - Булево - результат выполнения
Процедура ВыгрузитьКонфигурациюВФайл(Данные, Отказ) Экспорт 
	
	ПутьКВременнойБазе 	= ПолучитьПутьКВременнойБазе(Данные.Путь);
	Результат			= ВыполнитьКомандуСоздатьФайловуюБазу(Данные);
	
	Если Не Данные.Свойство("ПутьВременнойБазы") Тогда 
		Данные.Вставить("ПутьВременнойБазы", ПутьКВременнойБазе);	
	КонецЕсли;
	
	Если Результат <> 0 Тогда 
		Отказ = Истина;
	КонецЕсли;
	
	Если Не Отказ Тогда 
		
		//выгрузим в файл
		Результат = ВыполнитьКомандуВыгрузитьКонфигурациюВФайл(Данные);
		
		Если Результат <> 0 Тогда 
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	//удалим базу
	Ар_РаботаСКоманднойСтрокойКлиентСервер.УдалитьФайловуюБазу(ПутьКВременнойБазе);
	
КонецПроцедуры

// Функция - Выполнить команду выгрузить конфигурацию в файл
//
// Параметры:
//  Данные	 - Структура - Данные выполнения для команды
//		Приложение1с - Строка - Путь к 1с	
//		Путь - Строка - Каталог создания базы	
//		КаталогХранилища - Строка - Путь к хранилищу	
//		ПользовательХранилища - Строка - пользователь хранилища	
//		ПарольПользователяВХранилище - Строка - пароль пользователя хранилища	
//		ПутьФайла - Строка - Путь результата выгрузки	
// 
// Возвращаемое значение:
//  Результат - Число
//		0 - Успешно
//		Другое число - Ошибка
//"C:\Program Files (x86)\1cv8\common\1cestart.exe" DESIGNER /FC:\v8_temp /ConfigurationRepositoryFtcp://srv1/test /ConfigurationRepositoryNtest 
///ConfigurationRepositoryPtest /ConfigurationRepositoryDumpCfgC:\v8_temp\1c_v8.cf
Функция ВыполнитьКомандуВыгрузитьКонфигурациюВФайл(Данные) Экспорт 
	
	Результат = 999;
	
	ТекстКоманды	= СтрШаблон("""%1"" DESIGNER /F ""%2"" /ConfigurationRepositoryF ""%3"" /ConfigurationRepositoryN ""%4"" /ConfigurationRepositoryP ""%5"" /ConfigurationRepositoryDumpCfg ""%6""", 
						Данные.Приложение1с, Данные.ПутьВременнойБазы, Данные.КаталогХранилища, Данные.ПользовательХранилища, Данные.ПарольПользователяВХранилище, Данные.ПутьФайла);
	
	ЗапуститьПриложение(ТекстКоманды,, Истина, Результат);
	
	Возврат Результат
	
КонецФункции

#КонецОбласти

#Область Открытие1С

//"C:\Program Files\1cv82\8.2.11.235\bin\1cv8s.exe" ENTERPRISE /F 
//"D:\1C\HRM" /N user /P password /Execute "D:\1C\Внешние_обработки_1С\РаботающиеОбработки\РабочийСтол.epf"
Функция ПолучитьКомандуПоДаннымИнформационнойСистемы(Данные) Экспорт
	
	Шаблон 			= СтрШаблон("""%1"" %2", Данные.Платформа.Путь, Данные.Режим);
	ТипАвторизации 	= "WA";
	
	Если Данные.РасположениеИС.Тип = ПредопределенноеЗначение("Перечисление.Ар_ТипыРасположенияИнформационныйБазы.НаДанномКомпьютереИлиНаКомпьютереВЛокальнойСети") Тогда 
		Шаблон = СтрШаблон("%1 /F""%2""", Шаблон, Данные.РасположениеИС.Адрес);
	ИначеЕсли Данные.РасположениеИС.Тип = ПредопределенноеЗначение("Перечисление.Ар_ТипыРасположенияИнформационныйБазы.НаСервере1СПредприятия") Тогда  
		Шаблон = СтрШаблон("%1 /S""%2\%3""", Шаблон, Данные.РасположениеИС.Кластер, Данные.РасположениеИС.ИмяИС);
	ИначеЕсли Данные.РасположениеИС.Тип = ПредопределенноеЗначение("Перечисление.Ар_ТипыРасположенияИнформационныйБазы.НаВебСервере") Тогда  
		Шаблон = СтрШаблон("%1 /WS""%2""", Шаблон, Данные.РасположениеИС.Адрес);
		ТипАвторизации = "WSA";
	КонецЕсли;
	
	Шаблон = СтрШаблон("%1 /IBName""%2""", Шаблон, СтрЗаменить(Данные.Наименование, """", """"""));
	
	Если Данные.Авторизация.ВариантАутентификации = ПредопределенноеЗначение("Перечисление.Ар_ВариантыАутентификации.АутентификацияWindows") Тогда 
		Шаблон = СтрШаблон("%1 /%2+", Шаблон, ТипАвторизации);
	Иначе     		
		
		Если ЗначениеЗаполнено(Данные.Авторизация.Логин) Тогда 
			Шаблон = СтрШаблон("%1 /N""%2""", Шаблон, Данные.Авторизация.Логин);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Данные.Авторизация.Пароль) Тогда 
			Шаблон = СтрШаблон("%1 /P""%2""", Шаблон, Данные.Авторизация.Пароль);
		КонецЕсли;
		
		Шаблон = СтрШаблон("%1 /%2-", Шаблон, ТипАвторизации);

	КонецЕсли;
	
	Если ЗначениеЗаполнено(Данные.ДополнительныеПараметрыЗапуска) Тогда 
		Шаблон = СтрШаблон("%1 ""%2""", Шаблон, Данные.ДополнительныеПараметрыЗапуска);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Данные.ВнешняяОбработка) Тогда 
		Шаблон = СтрШаблон("%1 /Execute""%2""", Шаблон, Данные.ВнешняяОбработка);
	КонецЕсли;
	
	Шаблон = СтрШаблон("%1 /AppAutoCheckVersion–", Шаблон);
	
	Если Данные.Режим = "DESIGNER" Тогда 
		
		Если ЗначениеЗаполнено(Данные.Хранилище.Адрес) Тогда 
			Шаблон = СтрШаблон("%1 /ConfigurationRepositoryF""%2""", Шаблон, Данные.Хранилище.Адрес);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Данные.Хранилище.Логин) Тогда 
			Шаблон = СтрШаблон("%1 /ConfigurationRepositoryN""%2""", Шаблон, Данные.Хранилище.Логин);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Данные.Хранилище.Пароль) Тогда 
			Шаблон = СтрШаблон("%1 /ConfigurationRepositoryP""%2""", Шаблон, Данные.Хранилище.Пароль);
		КонецЕсли;
		
	ИначеЕсли Данные.Режим = "ENTERPRISE" Тогда 
		
		Если Данные.РежимЗапуска = ПредопределенноеЗначение("Перечисление.Ар_РежимЗапуска.ВыбиратьАвтоматически") Тогда 
			Шаблон = СтрШаблон("%1 /AppAutoCheckMode", Шаблон);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Шаблон
	
КонецФункции

#КонецОбласти
