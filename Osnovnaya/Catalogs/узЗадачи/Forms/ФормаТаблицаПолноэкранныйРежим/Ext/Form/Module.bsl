
// + #289 Попов 2022-09-14 @Badya67
&НаКлиенте
Перем ПропуститьВопросПриЗакрытии;

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ТабличныйДокумент") Тогда
		ТабличныйДокумент = Параметры.ТабличныйДокумент;
	КонецЕсли;
	
	Если Параметры.Свойство("ЗаголовокФормы") Тогда
		Заголовок = Параметры.ЗаголовокФормы;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПропуститьВопросПриЗакрытии = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не ЗавершениеРаботы И Не ПропуститьВопросПриЗакрытии Тогда
		
		Отказ = Истина;
		СтандартнаяОбработка = Ложь;
		
		Оповещение = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, "Сохранить изменения?", РежимДиалогаВопрос.ДаНетОтмена,, КодВозвратаДиалога.Да);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Результат, ДопПараметры) Экспорт
	
	ПропуститьВопросПриЗакрытии = Истина;
	
	Если Результат = КодВозвратаДиалога.Да Тогда		
		РезультатЗакрытия = Новый Структура();
		РезультатЗакрытия.Вставить("ТабличныйДокумент", ТабличныйДокумент);
		Закрыть(РезультатЗакрытия);
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Закрыть(Неопределено);
	ИначеЕсли Результат = КодВозвратаДиалога.Отмена Тогда
		ПропуститьВопросПриЗакрытии = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаТаблицаПолноэкранныйРежим(Команда)
	
	ПропуститьВопросПриЗакрытии = Истина;
	РезультатЗакрытия = Новый Структура();
	РезультатЗакрытия.Вставить("ТабличныйДокумент", ТабличныйДокумент);
	Закрыть(РезультатЗакрытия);
	
КонецПроцедуры
// - #289 Попов 2022-09-14 @Badya67
